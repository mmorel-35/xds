load("@com_google_protobuf//bazel:cc_proto_library.bzl", "cc_proto_library")
load("@io_bazel_rules_go//go:def.bzl", "go_test")
load("@rules_proto//proto:defs.bzl", "proto_library")
load(
    "//bazel:external_proto_deps.bzl",
    "EXTERNAL_PROTO_CC_BAZEL_DEP_MAP",
)

_CC_PROTO_SUFFIX = "_cc_proto"
_CC_GRPC_SUFFIX = "_cc_grpc"

_COMMON_PROTO_DEPS = [
    "@com_google_protobuf//:any_proto",
    "@com_google_protobuf//:descriptor_proto",
    "@com_google_protobuf//:duration_proto",
    "@com_google_protobuf//:empty_proto",
    "@com_google_protobuf//:struct_proto",
    "@com_google_protobuf//:timestamp_proto",
    "@com_google_protobuf//:wrappers_proto",
    "@com_google_googleapis//google/api:http_proto",
    "@com_google_googleapis//google/rpc:status_proto",
    "@protovalidate//proto/protovalidate/buf/validate:validate_proto",
]

def _proto_mapping(dep, proto_dep_map, proto_suffix):
    mapped = proto_dep_map.get(dep)
    if mapped == None:
        prefix = "@" + Label(dep).workspace_name if not dep.startswith("//") else ""
        return prefix + "//" + Label(dep).package + ":" + Label(dep).name + proto_suffix
    return mapped

def _cc_proto_mapping(dep):
    return _proto_mapping(dep, EXTERNAL_PROTO_CC_BAZEL_DEP_MAP, _CC_PROTO_SUFFIX)

def _xds_cc_proto_library(
        name,
        visibility = ["//visibility:private"],
        srcs = [],
        deps = [],
        has_services = 0):
    relative_name = ":" + name
    proto_library(
        name = name,
        srcs = srcs,
        deps = deps + _COMMON_PROTO_DEPS,
        visibility = visibility,
    )
    cc_proto_library_name = name + _CC_PROTO_SUFFIX
    cc_proto_library(
        name = cc_proto_library_name,
        deps = [relative_name, "@protovalidate-cc//buf/validate:validator"],
        visibility = ["//visibility:public"],
    )

    # Note: Python protobuf files are generated by Buf and available at //python:python_default_library
    # Note: Go protobuf files are generated by Buf and available at //go:go_default_library

    # Optionally define gRPC services
    if has_services:
        # TODO: neither C++ or Python service generation is supported today, follow the Envoy example to implement this.
        pass

def xds_proto_package(
        name = "pkg",
        srcs = [],
        deps = [],
        has_services = False,
        visibility = ["//visibility:public"]):
    if srcs == []:
        srcs = native.glob(["*.proto"])

    name = "pkg"
    _xds_cc_proto_library(
        name = name,
        visibility = visibility,
        srcs = srcs,
        deps = deps,
        has_services = has_services,
    )

    # Note: Go and Python protobuf generation is handled by Buf
    # Go files: //go:go_default_library
    # Python files: //python:python_default_library

def xds_cc_test(name, **kwargs):
    native.cc_test(
        name = name,
        **kwargs
    )

def xds_go_test(name, **kwargs):
    go_test(
        name = name,
        **kwargs
    )

# Old names for backward compatibility.
# TODO(roth): Remove these once all callers are migrated to the new names.
def udpa_proto_package(srcs = [], deps = [], has_services = False, visibility = ["//visibility:public"]):
    xds_proto_package(srcs = srcs, deps = deps, has_services = has_services, visibility = visibility)

def udpa_cc_test(name, **kwargs):
    xds_cc_test(name, **kwargs)

def udpa_go_test(name, **kwargs):
    xds_go_test(name, **kwargs)
